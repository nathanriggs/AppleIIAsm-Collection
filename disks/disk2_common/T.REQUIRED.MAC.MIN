_ISLIT   MAC
         IF    #=]1       ; IF ]1 IS A LITERAL
         LDA   ]1/$100    ; GET HI
         PHA
         LDA   ]1         ; GET LO
         PHA
         ELSE             ; ]1 IS ADDRESS
         LDA   ]1+1       ; SO GET HIGH VAL FROM ADDR
         PHA
         LDA   ]1         ; THEN LO VAL
         PHA
         FIN
         <<<
_ISSTR   MAC
         IF    "=]1 ; IF ]1 IS A STRING
         JMP   STRCONT
]STRTMP  STR   ]1
STRCONT
         LDY   #0
         LDA   ]STRTMP
         STA   PARLEN
         STA   PARAM
]STRLP1
         INY
         LDA   ]STRTMP,Y
         STA   PARAM,Y
         CPY   PARLEN
         BNE   ]STRLP1
         LDA   #>PARAM    ; GET HI
         PHA
         LDA   #<PARAM    ; GET LO
         PHA
         ELSE             ; ]1 IS ADDRESS
         LDA   #>]1       ; SO GET HIBYTE OF ADDR
         PHA
         LDA   #<]1       ; THEN LOBYTE
         PHA
         FIN
         <<<
_GRET    MAC
         LDA   #>]1
         PHA
         LDA   #<]1
         PHA
         JSR   __GETRET
         <<<
_SPAR    MAC
         LDA   #>]1
         PHA
         LDA   #<]1
         PHA
         JSR   __SETPARM
         <<<
_DUMP    MAC
         LDA   #>]1
         PHA
         LDA   #<]1
         PHA
         LDA   ]2
         PHA
         JSR   __DUMP
         <<<
_PRNT    MAC
         JSR   __P
         ASC   ]1
         HEX   00
         <<<
_WAIT    MAC
         JSR   __W
         <<<
_RDUMP   MAC
         JSR   __RDMP
         <<<
